
//===========================================================================
//
// Zombie man
//
//===========================================================================
actor ZombieMan native
{
    Health 20
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER
    SeeSound "posit1" // random 3
    AttackSound "pistol"
    PainSound "popain"
    DeathSound "podth1" // random 3
    ActiveSound "posact"
    DropItem "Clip"
    States
    {
    Spawn:
        POSS AB 10 A_Look
        Loop
    See:
        POSS AABBCCDD 4 A_Chase
        Loop
    Missile:
        POSS E 10 A_FaceTarget
        POSS F 8 A_PosAttack
        POSS E 8
        Goto See
    Pain:
        POSS G 3
        POSS G 3 A_Pain
        Goto See
    Death:
        POSS H 5
        POSS I 5 A_Scream
        POSS J 5 A_Fall
        POSS K 5
        POSS L -1
        Stop
    XDeath:
        POSS M 5
        POSS N 5 A_XScream
        POSS O 5 A_Fall
        POSS PQRST 5
        POSS U -1
        Stop
    Raise:
        POSS K 5
        POSS JIH 5
        Goto See
    }
}

//===========================================================================
//
// Sergeant / Shotgun guy
//
//===========================================================================
actor ShotgunGuy native
{
    Health 30
    Radius 20
    Height 56
    Mass 100
    Speed 8
    PainChance 170
    MONSTER
    SeeSound "posit1" // random 3
    AttackSound "shotgn"
    PainSound "popain"
    DeathSound "podth1" // random 3
    ActiveSound "posact"
    DropItem "Shotgun"
    States
    {
    Spawn:
        SPOS AB 10 A_Look
        Loop
    See:
        SPOS AABBCCDD 3 A_Chase
        Loop
    Missile:
        SPOS E 10 A_FaceTarget
        SPOS F 10 Bright A_SPosAttack
        SPOS E 10
        Goto See
    Pain:
        SPOS G 3
        SPOS G 3 A_Pain
        Goto See
    Death:
        SPOS H 5
        SPOS I 5 A_Scream
        SPOS J 5 A_Fall
        SPOS K 5
        SPOS L -1
        Stop
    XDeath:
        SPOS M 5
        SPOS N 5 A_XScream
        SPOS O 5 A_Fall
        SPOS PQRST 5
        SPOS U -1
        Stop
    Raise:
        SPOS L 5
        SPOS KJIH 5
        Goto See
    }
}

//===========================================================================
//
// Chaingunner
//
//===========================================================================
actor ChaingunGuy native
{
    Health 70
    Radius 20
    Height 56
    Mass 100
    Speed 8
    PainChance 170
    MONSTER
    SeeSound "posit1" // random 3
    PainSound "popain"
    DeathSound "podth1" // random 3
    ActiveSound "posact"
    AttackSound "shotgn"
    Dropitem "Chaingun"
    States
    {
    Spawn:
        CPOS AB 10 A_Look
        Loop
    See:
        CPOS AABBCCDD 3 A_Chase
        Loop
    Missile:
        CPOS E 10 A_FaceTarget
        CPOS FE 4 Bright A_CPosAttack
        CPOS F 1 A_CPosRefire
        Goto Missile+1
    Pain:
        CPOS G 3
        CPOS G 3 A_Pain
        Goto See
    Death:
        CPOS H 5
        CPOS I 5 A_Scream
        CPOS J 5 A_Fall
        CPOS KLM 5
        CPOS N -1
        Stop
    XDeath:
        CPOS O 5 
        CPOS P 5 A_XScream
        CPOS Q 5 A_Fall
        CPOS RS 5
        CPOS T -1
        Stop
    Raise:
        CPOS N 5
        CPOS MLKJIH 5
        Goto See
    }
}

//===========================================================================
//
// SS Nazi
//
//===========================================================================
actor WolfensteinSS native
{
    Health 50
    Radius 20
    Height 56
    Speed 8
    PainChance 170
    MONSTER
    SeeSound "sssit"
    PainSound "popain"
    DeathSound "ssdth"
    ActiveSound "posact"
    AttackSound "shotgn"
    Dropitem "Clip"
    States
    {
    Spawn:
        SSWV AB 10 A_Look
        Loop
    See:
        SSWV AABBCCDD 3 A_Chase
        Loop
    Missile:
        SSWV E 10 A_FaceTarget
        SSWV F 10 A_FaceTarget
        SSWV G 4 Bright A_CPosAttack
        SSWV F 6 A_FaceTarget
        SSWV G 4 Bright A_CPosAttack
        SSWV F 1 A_CPosRefire
        Goto Missile+1
    Pain:
        SSWV H 3
        SSWV H 3 A_Pain
        Goto See
    Death:
        SSWV I 5
        SSWV J 5 A_Scream
        SSWV K 5 A_Fall
        SSWV L 5
        SSWV M -1
        Stop
    XDeath:
        SSWV N 5 
        SSWV O 5 A_XScream
        SSWV P 5 A_Fall
        SSWV QRSTU 5
        SSWV V -1
        Stop
    Raise:
        SSWV M 5
        SSWV LKJI 5
        Goto See
    }
}

//===========================================================================
//
// Imp
//
//===========================================================================
actor DoomImp native
{
    Health 60
    Radius 20
    Height 56
    Mass 100
    Speed 8
    PainChance 200
    MONSTER
    SeeSound "bgsit1" // random 2
    PainSound "popain"
    DeathSound "bgdth1" // random 2
    ActiveSound "bgact"
    States
    {
    Spawn:
        TROO AB 10 A_Look
        Loop
    See:
        TROO AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        TROO EF 8 A_FaceTarget
        TROO G 6 A_TroopAttack
        Goto See
    Pain:
        TROO H 2
        TROO H 2 A_Pain
        Goto See
    Death:
        TROO I 8
        TROO J 8 A_Scream
        TROO K 6
        TROO L 6 A_Fall
        TROO M -1
        Stop
    XDeath:
        TROO N 5
        TROO O 5 A_XScream
        TROO P 5
        TROO Q 5 A_Fall
        TROO RST 5
        TROO U -1
        Stop
    Raise:
        TROO MLKJI 8
        Goto See
    }
}

//===========================================================================
//
// Imp fireball
//
//===========================================================================
actor DoomImpBall native
{
    Radius 6
    Height 8
    Speed 10
    FastSpeed 20
    Damage 3
    PROJECTILE
    SeeSound "firsht"
    DeathSound "firxpl"
    States
    {
    Spawn:
        BAL1 AB 4 Bright
        Loop
    Death:
        BAL1 CDE 6 Bright
        Stop
    }
}
