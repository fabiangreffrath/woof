# This script is stamped out by CMake and run by `make install`.

set(PROJECT_SOURCE_DIR "@PROJECT_SOURCE_DIR@")
set(CMAKE_CURRENT_SOURCE_DIR "@CMAKE_CURRENT_SOURCE_DIR@")
set(CMAKE_CURRENT_BINARY_DIR "@CMAKE_CURRENT_BINARY_DIR@")
set(Python3_EXECUTABLE "@Python3_EXECUTABLE@")
set(WIN32 "@WIN32@")

set(WOOF_TEXTS "@WOOF_DOCS_TEXTS@")

function(win_textfile SOURCE DESTINATION)
    if(Python3_EXECUTABLE)
        execute_process(COMMAND
            "${Python3_EXECUTABLE}" "${PROJECT_SOURCE_DIR}/win-textfile"
            "${SOURCE}" "${DESTINATION}")
    else()
        file(COPY "${SOURCE}" DESTINATION "${CMAKE_CURRENT_BINARY_DIR}")
    endif()
endfunction()

if(WIN32)
    if(NOT Python3_EXECUTABLE)
        message(WARNING "Python 3 not found, can't run install scripts.")
    endif()

    # Convert text files to a format Windows Notepad can read without issue.
    foreach(WOOF_TEXT ${WOOF_TEXTS})
        win_textfile(
            "${CMAKE_CURRENT_SOURCE_DIR}/${WOOF_TEXT}"
            "${CMAKE_CURRENT_BINARY_DIR}/${WOOF_TEXT}")
    endforeach()
endif()
